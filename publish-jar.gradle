apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

//项目主页
def siteUrl = 'https://juejin.im/post/59c6904df265da0656046be3'    // project homepage
//项目的版本控制地址
def gitUrl = 'https://github.com/lizhengxian1991/ButterFork.git' // project git

//发布到组织名称名字，必须填写
group = "top.lizhengxian"

//生成源文件
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

//上传到jcenter所需要的源码文件
artifacts {
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'jar'
                name 'This is a annotation just like ButterKnife'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'lizhengxian1991'
                        name 'zhengxian.lzx'
                        email 'lizhengxian1991@126.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }

            }
        }
    }
}

//上传到jcenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = project.ext.apt_libName    //发布到JCenter上的项目名字，必须填写
        desc = 'This is a annotation just like ButterKnife'    //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}